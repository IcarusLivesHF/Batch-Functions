@echo off & setlocal enableDelayedExpansion

set /a "gravity=1", "hei=wid=100", "objects=4", "tooManyObjects=objects+1"
set /a "frameRate=9 - (objects - (objects/2) - (objects/4))", "distanceSensitivity=6"
for /l %%o in (1,1,%objects%) do (
	set /a "x[%%o]=!random! %% wid", "y[%%o]=!random! %% hei", "mass[%%o]=1"
)
call :init
for /l %%# in () do ( set /a "frames+=1"
	for /l %%a in (1,1,%objects%) do (
		2>nul set /a "%every:n=frameRate%" && (
			2>nul set /a "%every:n=2%" && set /a "!gravityY:#=%%a!"
			2>nul set /a "!HorzEdgeDetection:x=x[%%a]!" && set /a "i[%%a]*=-1", "v_x[%%a]*=-1"
			2>nul set /a "!VertEdgeDetection:x=y[%%a]!" && set /a "j[%%a]*=-1", "v_y[%%a]*=-1"
			2>nul set /a "current=%%a", "next=current+1", "!if_Too_Many_Objects!" && set "next=1"
			set /a "x1=x[!current!]", "x2=x[!next!]", "y1=y[!current!]", "y2=y[!next!]", "distance[%%a]=%getDistance%"
			2>nul set /a "!ifObjectsAreTouching:x=distance[%%a]!" && set /a "i[!current!]*=-1", "i[!next!]*=-1", "j[!current!]*=-1", "j[!next!]*=-1"
		)
		set "screen=!screen!%esc%[!y[%%a]!;!x[%%a]!H!ball:CL=%%a!"
	)
	<nul set /p "=%esc%[?25l%esc%[2J!screen!" & set "screen="
)

:init
mode %wid%,%hei%
for /F %%a in ('echo prompt $E^| cmd') do set "ESC=%%a"
set "getDistance=( @=x2-x1, $=y2-y1, ?=(((@>>31|1)*@-(($>>31|1)*$))>>31)+1, ?*(2*(@>>31|1)*@-($>>31|1)*$-((@>>31|1)*@-($>>31|1)*$)) + ^^^!?*((@>>31|1)*@-($>>31|1)*$-((@>>31|1)*@-($>>31|1)*$*2)) )"
set "ifObjectsAreTouching=1/((~(distanceSensitivity-x)>>31)&1)"
set "if_Too_Many_Objects=1/(((~(tooManyObjects-next)>>31)&1)&((~(next-tooManyObjects)>>31)&1))"
set "every=1/(((~(0-(frames%%n))>>31)&1)&((~((frames%%n)-0)>>31)&1))"
set "HorzEdgeDetection=1/((((x-(wid-3))>>31)&1)^(((3-x)>>31)&1))"
set "VertEdgeDetection=1/((((x-(hei-3))>>31)&1)^(((3-x)>>31)&1))"
set "odds=1/((((^!random^!%%100)-x)>>31)&1)"
set "gravityX=a_x[#]+=(gravity * mass[#]), v_x[#]+=a_x[#], a_x[#]*=0, x[#]+=v_x[#]"
set "gravityY=a_y[#]+=(gravity * mass[#]), v_y[#]+=a_y[#], a_y[#]*=0, y[#]+=v_y[#]"
set "ball=%esc%[38;5;CLm 횤%ESC%[1B%ESC%[2D횤 횤%ESC%[1B%ESC%[2D횤"
goto :eof
